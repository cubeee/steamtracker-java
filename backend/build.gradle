buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://repo.spring.io/milestone' }
        maven { url 'http://repo.spring.io/plugins-release' }
    }

    dependencies {
        classpath group: 'org.springframework.build.gradle', name: 'propdeps-plugin', version: propDepsVersion
        classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: springBootVersion
    }
}

apply plugin: 'propdeps'
apply plugin: 'org.springframework.boot'

version = '1.0.0'

repositories {
    maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
    runtime project(path: ':frontend', configuration: 'archives')
    compile project(path: ':shared')
    compile deps.freemarkerInheritance
    compile deps.postgresql
    compile deps.flyway
    compile deps.jspApi
    compile deps.guava
    compile 'org.springframework.boot:spring-boot-devtools'
    compile 'org.springframework.boot:spring-boot-starter-freemarker'
    compile 'org.springframework.boot:spring-boot-starter-undertow'
    compile 'org.springframework.security:spring-security-openid'
    compile 'org.springframework.security:spring-security-taglibs'
    compile 'org.springframework:spring-webmvc'
    optional 'org.springframework.boot:spring-boot-configuration-processor'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

compileJava.dependsOn processResources

jar {
    archiveName = 'steamtracker-backend.jar'
    libsDirName = buildDir

    exclude('application-development.*')
    exclude('*.dist')
}

springBoot {
    backupSource = false
    mainClass = "com.x7ff.steam.SteamTracker"
}

bootRun {
    // https://youtrack.jetbrains.com/issue/IDEA-122577
    if (!project.plugins.collect { it.class.name }.any { it.endsWith('JetGradlePlugin') }) {
        sourceSets.main {
            resources {
                srcDir "${project(':frontend').buildDir}/dist"
            }
        }
    }

    addResources = true
}

bootRepackage.dependsOn check

task copyDependencies(type: Copy) {
    into "$rootDir/deploy/files/steam-tracker/libs"
    from configurations.runtime
}

task dockerBuildImage(type: Exec, dependsOn: "bootRepackage") {
    group = "docker"
    commandLine "docker", "build", "-t", "steamtracker:${project.version}", "."
}