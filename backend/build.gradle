buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://repo.spring.io/snapshot' }
        maven { url 'https://repo.spring.io/milestone' }
        maven { url 'http://repo.spring.io/plugins-release' }
    }

    dependencies {
        classpath group: 'org.springframework.build.gradle', name: 'propdeps-plugin', version: '0.0.7'
        classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: '1.4.0.BUILD-SNAPSHOT'
    }
}

apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'propdeps'
apply plugin: 'spring-boot'

repositories {
    maven { url 'https://repo.spring.io/snapshot' }
    maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
    runtime project(path: ':frontend', configuration: 'archives')
    compile deps.freemarkerInheritance
    compile deps.postgresql
    compile deps.javaxInject
    compile deps.jsoup
    compile 'org.springframework.boot:spring-boot-devtools'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-freemarker'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'com.fasterxml.jackson.core:jackson-databind'
    compile 'org.flywaydb:flyway-core'
    optional 'org.springframework.boot:spring-boot-configuration-processor'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

compileJava.dependsOn processResources

jar {
    baseName = 'steam-tracker'
    libsDirName = file("$rootDir/deploy/opt/steam-tracker")
}

springBoot {
    backupSource = false
}

bootRun {
    // https://youtrack.jetbrains.com/issue/IDEA-122577
    if (!project.plugins.collect { it.class.name }.any { it.endsWith('JetGradlePlugin') }) {
        sourceSets.main {
            resources {
                srcDir "${project(':frontend').buildDir}/dist"
            }
        }
    }

    /*jvmArgs = [
            '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005'
    ]*/

    addResources = true
}

bootRepackage.dependsOn check