plugins {
    id 'com.moowork.node' version '0.12'
}

apply plugin: 'base'

node {
    version = '7.9.0'
    download = true
}

task semanticBuild(type: NpmTask) {
    group = 'Node'
    description = 'Build Semantic UI'
    args = ['run', 'build-semantic']
}

task webpack(type: NpmTask, dependsOn: "npmInstall") {
    group = 'Node'
    description = 'Pack frontend resources with webpack'
    args = ['run', 'build']
    inputs.dir 'src'
    inputs.file 'webpack.config.js'
    outputs.dir "$distsDir/${project.name}"
}

task webpackWatch(type: NpmTask) {
    group = 'Node'
    description = 'Watch frontend changes with webpack'
    args = ['run', 'watch']
}

task jar(type: Jar) {
    group = LifecycleBasePlugin.BUILD_GROUP
    description = 'Package frontend resources into a jar'

    from tasks.webpack

    eachFile { file ->
        file.path = file.path.startsWith("META-INF") ?: "${project.name}/" + file.path
    }

    includeEmptyDirs = false
}

artifacts {
    archives jar
}